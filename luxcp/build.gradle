plugins {
  id 'java'
  id 'maven'
  id 'signing'
}

sourceCompatibility = targetCompatibility = '1.8'

group = 'se.llbit'
version = '1.0.1'

compileJava.options.encoding = 'UTF-8'

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  required { project.hasProperty('signing.keyId') }
  sign configurations.archives
}

if (project.hasProperty('ossrhUsername')) {
  uploadArchives {
    repositories.mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'LuxColorPicker'
        packaging 'jar'
        description 'HSV color picker for JavaFX.'
        url 'http://llbit.se'
        licenses {
          license {
            name 'Modified BSD License'
            url 'http://opensource.org/licenses/BSD-3-Clause'
            distribution 'repo'
          }
        }
        developers {
          developer {
            name 'Jesper Ã–qvist'
            email 'jesper@llbit.se'
          }
        }
        scm {
          connection 'scm:git:https://github.com/llbit/luxcp.git'
          url 'https://github.com/llbit/luxcp'
        }
      }
    }
  }
}
